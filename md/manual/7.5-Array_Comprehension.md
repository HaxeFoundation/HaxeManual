## 7.5 Array Comprehension

Array comprehension in Haxe uses existing syntax to allow concise initialization of arrays. It is identified by `[for` or `[while` constructs:

```haxe
class Main {
	static public function main() {
		var a = [for (i in 0...10) i];
		trace(a); // [0,1,2,3,4,5,6,7,8,9]
		
		var i = 0;
		var b = [while(i < 10) i++];
		trace(b); // [0,1,2,3,4,5,6,7,8,9]
	}
}
```

Variable `a` is initialized to an array holding the numbers 0 to 9. The compiler generates code which adds the value of each loop iteration to the array, so the following code would be equivalent:

```haxe
var a = [];
for (i in 0...10) a.push(i);
```

Variable `b` is initialized to an array with the same values, but through a different comprehension style using `while` instead of `for`. Again, the following code would be equivalent:

```haxe
var i = 0;
var a = [];
while(i < 10) a.push(i++);
```

The loop expression can be anything, including conditions and nested loops, so the following works as expected:

>TODO: maybe change this example around so it's easier to follow.

```haxe
class Main {
	static public function main() {
		var a = [
			for (a in 1...11)
				for(b in 2...Std.int(a / 2) + 1)
					if (a % b == 0)
						a+ "/" +b
		];
		trace(a); // [4/2,6/2,6/3,8/2,8/4,9/3,10/2,10/5]
	}
}
```

---

Previous section: [String Interpolation](7.4-String_Interpolation.md)

Next section: [Dead Code Elimination](7.6-Dead_Code_Elimination.md)