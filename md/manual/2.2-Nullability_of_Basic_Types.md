## 2.2 Nullability of Basic Types

> ##### Define: nullable
>
> A type in haxe is considered **nullable** if `null` is a valid value for it.


It is common for programming languages to have a single, clean definition for nullability, but haxe has to find a compromise in this regard. Some target languages allow and, in fact, default to `null` for everything, other do not allow `null` for certain types. This necessitates the distinction of two types of target languages:

> ##### Define: Static target
>
> `null` is not a valid value for basic types. This is true for the Flash 9+, C++, Java and C# targets.

> ##### Define: Dynamic target
>
> Dynamic targets are more lenient with their types and allow null values for basic types. They consist of JavaScript, PHP, neko and Flash 6-8.


There is nothing to worry about when working with `null` on dynamic targets, but static ones may require some thought. For starters, basic types are initialized to these values:



* `Int`: `0`
* `Float`: `NaN` on Flash 9, `0.0` on other static targets
* `Bool`: `false`


As a consequence, the haxe compiler does not allow assigning `null` to a basic type on static targets. In order to achieve this, the basic type has to be wrapped as `Null<T>`.  The **<T>** is a type parameter.  See [Type Parameters](3.2-Type_Parameters.md) for more details. 

```haxe
var a:Int = null; // error on static platforms
var b:Null<Int> = null; // allowed
```
Similarly, basic types cannot be compared to `null` unless wrapped:

```haxe
var a : Int = 0;
if( a == null ) { ... } // error on static platforms
var b : Null<Int> = 0;
if( b != null ) { ... } // allowed
```
This restriction extends to all situations where [unification](3.5-Unification.md) is performed.

If a `null`-value is "hidden" in `Null<T>` or `Dynamic` and assigned to a basic type, the default value is used:

```haxe
var n : Null<Int> = null;
var a : Int = n;
trace(a); // 0 on static platforms
```

**Optional Parameters and Nullability:**

>TODO: Optional parameters is referred to as "optional Arguments" later in the manual.  For consistency sake, do we prefer parameter or argument?

Optional parameters also have to be accounted when considering nullability.
In particular, there must be a distinction between **native** optional parameters which are not nullable and Haxe optional parameters which might be.  The distinction is made by using the question-mark optional parameter:

```haxe
// x is a native Int (not nullable)
function foo(x : Int = 0) {...}
// y is Null<Int> (nullable)
function bar( ?y : Int) {...}
// z is also Null<Int>
function opt( ?z : Int = -1) {...}
```

---

Previous section: [Void](2.1.4-Void.md)

Next section: [Class Instance](2.3-Class_Instance.md)