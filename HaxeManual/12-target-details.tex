\chapter{Target Details}
\label{target-details}
\state{NoContent}

\section{JavaScript}
\label{target-javascript}
\state{NoContent}

\subsection{Getting started with Haxe/JavaScript}
\label{target-javascript-getting-started}

Haxe can be a powerful tool for developing JavaScript applications. Let's look at our first sample.
This is a very simple example showing the toolchain. 

Create a new folder and save this class as \ic{Main.hx}.

\begin{lstlisting}
import js.Browser;
class Main {
    static function main() {
        var button = Browser.document.createButtonElement();
        button.textContent = "Click me!";
        button.onclick = function(event) {
            Browser.alert("Haxe is great");
        }
        Browser.document.body.appendChild(button);
    }
}
\end{lstlisting}

To compile, either run the following from the command line:

\begin{lstlisting}
haxe -js main-javascript.js -main Main -D js-flatten -dce full
\end{lstlisting}

Another possibility is to create and run (double-click) a file called \ic{compile.hxml}. In this example the hxml-file should be in the same directory as the example class.

\begin{lstlisting}
-js main-javascript.js
-main Main
-D js-flatten
-dce full
\end{lstlisting}

The output will be a main-javascript.js, which creates and adds a clickable button to the document body.

\paragraph{Run the JavaScript}

To display the output in a browser, create an HTML-document called \ic{index.html} and open it.

\begin{lstlisting}
<!DOCTYPE html>
<html>
	<body>
		<script src="main-javascript.js"></script>
	</body>
</html>
\end{lstlisting}

\paragraph{More information}

\begin{itemize}
	\item \href{http://api.haxe.org/js/}{Haxe JavaScript API docs}
	\item \href{https://developer.mozilla.org/en-US/docs/Web/JavaScript/Reference}{MDN JavaScript Reference}
\end{itemize}

\subsection{Using external JavaScript libraries}
\label{target-javascript-external-libraries}

The \tref{externs mechanism}{lf-externs} provides access to the native APIs in a type-safe manner. It assumes that the defined types exist at run-time but assumes nothing about how and where those types are defined. 

An example of an extern class is the \href{https://github.com/HaxeFoundation/haxe/blob/development/std/js/jquery/JQuery.hx}{jQuery class} of the Haxe Standard Library. 
To illustrate, here is a simplified version of this extern class:

\begin{lstlisting}
package js.jquery;
@:native("$") extern class JQuery {
	/**
		Creates DOM elements on the fly from the provided string of raw HTML.
		OR
		Accepts a string containing a CSS selector which is then used to match a set of elements.
		OR
		Binds a function to be executed when the DOM has finished loading.
	**/
	@:selfCall
	@:overload(function(element:js.html.Element):Void { })
	@:overload(function(selection:js.jquery.JQuery):Void { })
	@:overload(function(callback:haxe.Constraints.Function):Void { })
	@:overload(function(selector:String, ?context:haxe.extern.EitherType<js.html.Element, js.jquery.JQuery>):Void { })
	public function new():Void;

	/**
		Adds the specified class(es) to each element in the set of matched elements.
	**/
	@:overload(function(_function:Int -> String -> String):js.jquery.JQuery { })
	public function addClass(className:String):js.jquery.JQuery;

	/**
		Get the HTML contents of the first element in the set of matched elements.
		OR
		Set the HTML contents of each element in the set of matched elements.
	**/
	@:overload(function(htmlString:String):js.jquery.JQuery { })
	@:overload(function(_function:Int -> String -> String):js.jquery.JQuery { })
	public function html():String;
}
\end{lstlisting}

Note that functions can be overloaded to accept different types of arguments and return values, using the \expr{@:overload} metadata. Function overloading works only in externs.

Using this extern, we can use jQuery like this:

\begin{lstlisting}
import js.jquery.*;
..
new JQuery("#my-div").addClass("brand-success").html("haxe is great!");
..
\end{lstlisting}

The package and class name of the extern class should be the same as defined in the external library. If that is not the case, rewrite the path of a class using \expr{@:native}.

\begin{lstlisting}
package my.application.media;

@:native('external.library.media.video')
extern class Video {
..
\end{lstlisting}

Some JavaScript libraries favor instantiating classes without using the \expr{new} keyword. To prevent the Haxe compiler outputting the \expr{new} keyword when using a class, we can attach a \expr{@:selfCall} metadata to its constructor. For example, when we instantiate the jQuery extern class above, \expr{new JQuery()} will be outputted as \expr{\$()} instead of \expr{new \$()}. The \expr{@:selfCall} metadata can also be attached to a method. In this case, the method will be interpreted as a direct call to the object, illustrated as follows:

\begin{lstlisting}
extern class Functor {
	public function new():Void;
	@:selfCall function call():Void;
}

class Test {
	static function main() {
		var f = new Functor();
		f.call(); // will be outputted as `f();`
	}
}
\end{lstlisting}

Beside externs, \tref{Typedefs}{type-system-typedef} can be another great way to name (or alias) a JavaScript type. The major difference between typedefs and externs is that, typedefs are duck-typed but externs are not. Typedefs are suitable for common data structures, e.g. point (\expr{\{x:Float, y:Float\}}). Use of a point structure typedef for function arguments allows external JavaScript functions to accept point class instances from Haxe or from another JavaScript library. It is also useful for typing JSON objects.

The Haxe Standard Library comes with externs of \href{https://jquery.com/}{jQuery} and \href{http://blog.deconcept.com/swfobject/}{SWFObject}. Their version compatibility is summarized as follows:

\begin{center}
\begin{tabular}{| l | l | l |}
	\hline
	Haxe version & Library               & Externs location \\ \hline
	3.3          & jQuery 1.11.3 / 2.1.4 & <code>js.jquery.*</code> \\
	3.2-         & jQuery 1.6.4          & <code>js.JQuery</code> \\
	3.3          & SWFObject 2.3         & <code>js.swfobject.*</code> \\
	3.2-         & SWFObject 1.5         & <code>js.SWFObject</code> \\ \hline
\end{tabular}
\end{center}

There are many externs for other popular native libraries available on \tref{Haxelib library}{haxelib}. To view a list of them, check out the \href{http://lib.haxe.org/t/extern/}{extern tag}.

\subsection{Inject raw JavaScript}
\label{target-javascript-injection}

In Haxe, it is possible to call an exposed function thanks to the \expr{untyped} keyword. This can be useful in some cases if we don't want to write externs. Anything untyped that is valid syntax will be generated as it is.

\begin{lstlisting}
untyped window.trackEvent("page1");  
\end{lstlisting}


\subsection{JavaScript untyped functions}
\label{target-javascript-untyped}

These functions allow to access specific JavaScript platform features. It works only when the Haxe compiler is targeting JavaScript and should always be prefixed with \expr{untyped}. 

\emph{Important note:} Before using these functions, make sure there is no alternative available in the Haxe Standard Library. The resulting syntax can not be validated by the Haxe compiler, which may result in invalid or error-prone code in the output.

\paragraph{\expr{untyped __js__(expr, params)}}
\tref{Injects raw JavaScript expressions}{target-javascript-injection}. It's allowed to use \ic{\{0\}}, \ic{\{1\}}, \ic{\{2\}} etc in the expression and use the rest arguments to feed Haxe fields. The Haxe compiler will take care of the surrounding quotes if needed. The function can also return values.

\begin{lstlisting}
untyped __js__('alert("Haxe is great!")');
// output: alert("Haxe is great!");

var myMessage = "Haxe is great!";
untyped __js__('alert({0})', myMessage);
// output: 
//	var myMessage = "Haxe is great!";
//	alert(myMessage);

var myVar:Bool = untyped __js__('confirm({0})', "Are you sure?");
// output: var myVar = confirm("Are you sure?");

var hexString:String = untyped __js__('({0}).toString({1})', 255, 16);
// output: var hexString = (255).toString(16);
\end{lstlisting}

\paragraph{\expr{untyped __instanceof__(o,cl)}} 
Same as \ic{o instanceof cl} in JavaScript.

\begin{lstlisting}
var myString = new String("Haxe is great");
var isString = untyped __instanceof__(myString, String);
output: var isString = (myString instanceof String);
\end{lstlisting}

\paragraph{\expr{untyped __typeof__(o)}} 
Same as \ic{typeof o} in JavaScript.

\begin{lstlisting}
var isNodeJS = untyped __typeof__(window) == null;
output: var isNodeJS = typeof(window) == null;
\end{lstlisting}

\paragraph{\expr{untyped __strict_eq__(a,b)}} 
Same as \ic{a === b}  in JavaScript, tests on \href{https://developer.mozilla.org/en-US/docs/Web/JavaScript/Equality_comparisons_and_sameness}{strict equality} (or "triple equals" or "identity").

\begin{lstlisting}
var a = "0";
var b = 0;
var isEqual = untyped __strict_eq__(a, b);
output: var isEqual = ((a) === b);
\end{lstlisting}

\paragraph{\expr{untyped __strict_neq__(a,b)}} 
Same as \ic{a !== b}  in JavaScript, tests on negative strict equality.

\begin{lstlisting}
var a = "0";
var b = 0;
var isntEqual = untyped __strict_neq__(a, b);
output: var isntEqual = ((a) !== b);
\end{lstlisting}

\paragraph{Expression injection} 

In some cases it may be needed to inject raw JavaScript code into Haxe-generated code. With the \expr{__js__} function we can inject pure JavaScript code fragments into the output. This code is always untyped and can not be validated, so it accepts invalid code in the output, which is error-prone.
This could, for example, write a JavaScript comment in the output.

\begin{lstlisting}
untyped __js__('// haxe is great!');
\end{lstlisting}

A more useful demonstration would be to call a function and pass  arguments using the \expr{__js__} function. This example illustrates how to call this function and how to pass parameters. Note that the \emph{code interpolation} will wrap the quotes around strings in the generated output.

\begin{lstlisting}
// Haxe code:
var myVar = untyped __js__('myObject.myJavaScriptFunction({0}, {1})', "Mark", 31);
\end{lstlisting}

This will generate the following JavaScript code:
\begin{lstlisting}
// JavaScript Code
var myVar = myObject.myJavaScriptFunction("Mark", 31);
\end{lstlisting}

\subsection{Debugging JavaScript}
\label{target-javascript-debugging}

Haxe is able to generate \href{http://www.html5rocks.com/en/tutorials/developertools/sourcemaps/}{source maps}, allowing Javascript debuggers to map from generated JavaScript back to the original Haxe source. This makes reading error stack traces, debugging with breakpoints, and profiling much easier.

Compiling with the \ic{-debug} flag will create a .map alongside the .js file. Enable it in Chrome by clicking on the cog settings button in the bottom right of the Developer Tools window, and checking "Enable source maps". The pause button on the bottom left can be toggled to pause on uncaught exceptions.

\subsection{JavaScript target Metadata}
\label{target-javascript-metadata}

This is the list of JavaScript specific metadata. For more information, see also the complete list of all \tref{Haxe built-in metadata}{cr-metadata}.

\begin{center}
\begin{tabular}{| l | l |}
	\hline
	\multicolumn{2}{|c|}{JavaScript metadata} \\ \hline
	Metadata &  Description \\ \hline
	@:expose \_(?Name=Class path)\_  &  Makes the class available on the \expr{window} object or \expr{exports} for node.js  \\
	@:jsRequire  &  Generate javascript module require expression for given extern \\
	@:selfCall  &  Translates method calls into calling object directly \\
\end{tabular}
\end{center}

\subsection{Exposing Haxe classes for JavaScript}
\label{target-javascript-expose}

It is possible to make Haxe classes or static fields available for usage in plain JavaScript. 
To expose, add the \expr{@:expose} metadata to the desired class or static fields.

This example exposes the Haxe class \ic{MyClass}.

\haxe{assets/ClassExpose.hx}

It generates the following JavaScript output:

\begin{lstlisting}
(function ($hx_exports) { "use strict";
var MyClass = $hx_exports.MyClass = function(name) {
	this.name = name;
};
MyClass.prototype = {
	foo: function() {
		return "Greetings from " + this.name + "!";
	}
};
})(typeof window != "undefined" ? window : exports);
\end{lstlisting}

By passing globals (like \ic{window} or \ic{exports}) as parameters to our anonymous function in the JavaScript module, it becomes available which allows to expose the Haxe generated module.

In plain JavaScript it is now possible to create an instance of the class and call its public functions.

\begin{lstlisting}
// JavaScript code
var instance = new MyClass('Mark');
console.log(instance.foo()); // logs a message in the console
\end{lstlisting}

The package path of the Haxe class will be completely exposed. To rename the class or define a different package for the exposed class, use \expr{@:expose("my.package.MyExternalClass")}

\paragraph{Shallow expose}

When the code generated by Haxe is part of a larger JavaScript project and wrapped in a large closure it is not always necessary to expose the Haxe types to global variables.
Compiling the project using \ic{-D shallow-expose} allows the types or static fields to be available for the surrounding scope of the generated closure only.

When the code is compiled using \ic{-D shallow-expose}, the generated output will look like this:

\begin{lstlisting}
var $hx_exports = $hx_exports || {};
(function () { "use strict";
var MyClass = $hx_exports.MyClass = function(name) {
	this.name = name;
};
MyClass.prototype = {
	foo: function() {
		return "Greetings from " + this.name + "!";
	}
};
})();
var MyClass = $hx_exports.MyClass;
\end{lstlisting}

In this pattern, a var statement is used to expose the module; it doesn't write to the \ic{window} or \ic{exports} object. 

\subsection{Loading extern classes using "require" function}
\label{target-javascript-require}
\since{3.2.0}

Modern \target{JavaScript} platforms, such as Node.js provide a way of loading objects
from external modules using the "require" function. Haxe supports automatic generation
of "require" statements for \expr{extern} classes.

This feature can be enabled by specifying \expr{@:jsRequire} metadata for the extern class. If our \expr{extern} class represents a whole module, we pass a single argument to the \expr{@:jsRequire} metadata specifying the name of the module to load:

\haxe{assets/JSRequireModule.hx}

In case our \expr{extern} class represents an object within a module, second \expr{@:jsRequire} argument specifies an object to load from a module:

\haxe{assets/JSRequireObject.hx}

The second argument is a dotted-path, so we can load sub-objects in any hierarchy.

If we need to load custom JavaScript objects in runtime, a \expr{js.Lib.require} function can be used. It takes \expr{String} as its only argument and returns \expr{Dynamic}, so it is advised to be assigned to a strictly typed variable.

\section{Flash}
\label{target-flash}
\state{NoContent}

\subsection{Getting started with Haxe/Flash}
\label{target-flash-getting-started}

Developing Flash applications is really easy with Haxe. Let's look at our first code sample.
This is a basic example showing most of the toolchain. 

Create a new folder and save this class as \ic{Main.hx}.

\begin{lstlisting}
import flash.Lib;
import flash.display.Shape;
class Main {
    static function main() {
        var stage = Lib.current.stage;
        
        // create a center aligned rounded gray square
        var shape = new Shape();
        shape.graphics.beginFill(0x333333);
		shape.graphics.drawRoundRect(0, 0, 100, 100, 10);
		shape.x = (stage.stageWidth - 100) / 2;
		shape.y = (stage.stageHeight - 100) / 2;
		
		stage.addChild(shape);
    }    
}
\end{lstlisting}

To compile this, either run the following from the command line:

\begin{lstlisting}
haxe -swf main-flash.swf -main Main -swf-version 15 -swf-header 960:640:60:f68712
\end{lstlisting}

Another possibility is to create and run (double-click) a file called \ic{compile.hxml}. In this example the hxml-file should be in the same directory as the example class.

\begin{lstlisting}
-swf main-flash.swf
-main Main
-swf-version 15
-swf-header 960:640:60:f68712
\end{lstlisting}

The output will be a main-flash.swf with size 960x640 pixels at 60 FPS with an orange background color and a gray square in the center.

\paragraph{Display the Flash}

Run the SWF standalone using the \href{https://www.adobe.com/support/flashplayer/downloads.html}{Standalone Debugger FlashPlayer}. 

To display the output in a browser using the Flash-plugin, create an HTML-document called \ic{index.html} and open it.

\begin{lstlisting}
<!DOCTYPE html>
<html>
	<body>
		<embed src="main-flash.swf" width="960" height="640">
	</body>
</html>
\end{lstlisting}

\paragraph{More information}

\begin{itemize}
	\item \href{http://api.haxe.org/flash/}{Haxe Flash API docs}
	\item \href{http://help.adobe.com/en_US/FlashPlatform/reference/actionscript/3/}{Adobe Livedocs}
\end{itemize}

\subsection{Embedding resources}
\label{target-flash-resources}

Embedding resources is different in Haxe compared to ActionScript 3. Instead of using \ic{\[embed\]} (AS3-metadata) use \tref{Flash specific compiler metadata}{target-flash-metadata} like \ic{@:bitmap}, \ic{@:font}, \ic{@:sound} or \ic{@:file}.

\begin{lstlisting}
import flash.Lib;
import flash.display.BitmapData;
import flash.display.Bitmap;

class Main {
  public static function main() {
    var img = new Bitmap( new MyBitmapData(0, 0) );
    Lib.current.addChild(img);
  }
}

@:bitmap("relative/path/to/myfile.png") 
class MyBitmapData extends BitmapData { }
\end{lstlisting}

\subsection{Using external Flash libraries}
\label{target-flash-external-libraries}

To embed external \ic{.swf} or \ic{.swc} libraries, use the following \href{http://haxe.org/documentation/introduction/compiler-usage.html}{compilation options}:

\begin{description}
	\item[\expr{-swf-lib <file>}] Embeds the SWF library in the compiled SWF.
	\item[\expr{-swf-lib-extern <file>}] Adds the SWF library for type checking but doesn't include it in the compiled SWF.
\end{description}

The standard compilation options also provide more Haxe sources to be added to the project:

\begin{itemize}
	\item To add another class path use \expr{-cp <directory>}.
	\item To add a \tref{Haxelib library}{haxelib} use \expr{-lib <library-name>}.
	\item To force a whole package to be included in the project, use \expr{--macro include('mypackage')} which will include all the classes declared in the given package and subpackages. 
\end{itemize}

\subsection{Flash target Metadata}
\label{target-flash-metadata}

This is the list of Flash specific metadata. For a complete list see \tref{Haxe built-in metadata}{cr-metadata}.

\begin{center}
\begin{tabular}{| l | l |}
	\hline
	\multicolumn{2}{|c|}{Flash metadata} \\ \hline
	Metadata &  Description  \\ \hline
	@:bind  &  Override Swf class declaration \\
	@:bitmap \_(Bitmap file path)\_  &  \_Embeds given bitmap data into the class (must extend \expr{flash.display.BitmapData}) \\
	@:debug  &  Forces debug information to be generated into the Swf even without \expr{-debug} \\
	@:file(File path)  &  Includes a given binary file into the target Swf and associates it with the class (must extend \expr{flash.utils.ByteArray}) \\
	@:font \_(TTF path Range String)\_  &  Embeds the given TrueType font into the class (must extend \expr{flash.text.Font}) \\
	@:getter \_(Class field name)\_  &  Generates a native getter function on the given field  \\
	@:noDebug &  Does not generate debug information into the Swf even if \expr{-debug} is set \\
	@:ns  &  Internally used by the Swf generator to handle namespaces \\
	@:setter \_(Class field name)\_  &  Generates a native setter function on the given field \\
	@:sound \_(File path)\_  &  Includes a given \_.wav\_ or \_.mp3\_ file into the target Swf and associates it with the class (must extend \expr{flash.media.Sound}) \\
\end{tabular}
\end{center}

\section{Neko}
\label{target-neko}

\section{PHP}
\label{target-php}
\state{NoContent}

\subsection{Getting started with Haxe/PHP}
\label{target-php-getting-started}

To get started with Haxe/PHP, create a new folder and save this class as \ic{Main.hx}.

\haxe{assets/HelloPHP.hx}

To compile, either run the following from the command line:

\begin{lstlisting}
haxe -php bin -main Main
\end{lstlisting}

Another possibility is to create and run (double-click) a file called \ic{compile.hxml}. In this example the hxml-file should be in the same directory as the example class.

\begin{lstlisting}
-php bin
-main Main
\end{lstlisting}

The compiler outputs in the given \emph{bin}-folder, which contains the generated PHP classes that prints the traced message when you run it. The generated PHP-code runs for version 5.1.0 and later.

\paragraph{More information}

\begin{itemize}
	\item \href{http://api.haxe.org/php/}{Haxe PHP API docs}
	\item \href{http://php.net/docs.php}{PHP.net Documentation}
	\item \href{http://phptohaxe.haqteam.com/code.php}{PHP to Haxe tool}
\end{itemize}


\subsection{PHP untyped functions}
\label{target-php-untyped}

These functions allow to access specific PHP platform features. It works only when the Haxe compiler is targeting PHP and should always be prefixed with \expr{untyped}. 

\emph{Important note:} Before using these functions, make sure there is no alternative available in the Haxe Standard Library. The resulting syntax can not be validated by the Haxe compiler, which may result in invalid or error-prone code in the output.

\paragraph{\expr{untyped __php__(expr)}}
Injects raw PHP code expressions. It's possible to pass fields from Haxe source code using \tref{String Interpolation}{lf-string-interpolation}.

\begin{lstlisting}
var value:String = "test";
untyped __php__("echo '<pre>'; print_r($value); echo '</pre>';");
// output: echo '<pre>'; print_r('test'); echo '</pre>';
\end{lstlisting}

\paragraph{\expr{untyped __call__(function, arg, arg, arg...)}}
Calls a PHP function with the desired number of arguments and returns what the PHP function returns.

\begin{lstlisting}
var value = untyped __call__("array", 1,2,3); 
// output returns a NativeArray with values [1,2,3]
\end{lstlisting}

\paragraph{\expr{untyped __var__(global, paramName)}}
Get the values from global vars. Note that the dollar sign in the Haxe code is omitted.

\begin{lstlisting}
var value : String = untyped __var__('_SERVER', 'REQUEST_METHOD')  
// output: $value = $_SERVER['REQUEST_METHOD']
\end{lstlisting}

\paragraph{\expr{untyped __physeq__(val1, val2)}}
Strict equals test between the two values. Returns a \type{Bool}.

\begin{lstlisting}
var isFalse = untyped __physeq__(false, value);
// output: $isFalse = false === $value;
\end{lstlisting}


\section{C++}
\label{target-cpp}
\state{NoContent}

\subsection{Using C++ Defines}
\label{target-cpp-defines}
\begin{itemize}
	\item ANDROID_HOST
	\item ANDROID_NDK_DIR
	\item ANDROID_NDK_ROOT
	\item BINDIR
	\item DEVELOPER_DIR
	\item HXCPP
	\item HXCPP_32
	\item HXCPP_COMPILE_CACHE
	\item HXCPP_COMPILE_THREADS
	\item HXCPP_CONFIG
	\item HXCPP_CYGWIN
	\item HXCPP_DEPENDS_OK
	\item HXCPP_EXIT_ON_ERROR
	\item HXCPP_FORCE_PDB_SERVER
	\item HXCPP_M32
	\item HXCPP_M64
	\item HXCPP_ARMV6
	\item HXCPP_ARMV7
	\item HXCPP_ARMV7S
	\item HXCPP_ARM64
	\item HXCPP_X86
	\item HXCPP_MINGW
	\item HXCPP_MSVC
	\item HXCPP_MSVC_CUSTOM
	\item HXCPP_MSVC_VER
	\item HXCPP_NO_COLOR
	\item HXCPP_NO_COLOUR
	\item HXCPP_VERBOSE
	\item HXCPP_WINXP_COMPAT
	\item HXCPP_API_LEVEL
	\item HXCPP_DEBUG_LINK
	\item DHXCPP_STACK_TRACE
	\item HXCPP_STACK_LINE
	\item HXCPP_CHECK_POINTER
	\item HXCPP_DEBUGGER
	\item HXCPP_CPP11
	\item HXCPP_STRICT_CASTS
	\item HXCPP_VISIT_ALLOCS
	\item HXCPP_WINXP_COMPAT
	\item IPHONE_VER
	\item LEGACY_MACOSX_SDK
	\item LEGACY_XCODE_LOCATION
	\item MACOSX_VER
	\item MSVC_VER
	\item NDKV
	\item NO_AUTO_MSVC
	\item PLATFORM
	\item QNX_HOST
	\item QNX_TARGET
	\item TOOLCHAIN_VERSION
	\item USE_GCC_FILETYPES
	\item USE_PRECOMPILED_HEADERS
	\item android
	\item apple
	\item blackberry
	\item cygwin
	\item dll_import
	\item dll_import_include
	\item dll_import_link
	\item emcc
	\item emscripten
	\item gph
	\item hardfp
	\item haxe_ver
	\item ios
	\item iphone
	\item iphoneos
	\item iphonesim
	\item linux
	\item linux_host
	\item mac_host
	\item macos
	\item mingw
	\item rpi
	\item simulator
	\item tizen
	\item toolchain
	\item webos
	\item windows
	\item windows_host
	\item winrt
	\item xcompile
\end{itemize}

\subsection{Using C++ Pointers}
\label{target-cpp-pointers}

\section{Java}
\label{target-java}

\section{C\#}
\label{target-cs}

\section{Python}
\label{target-python}
\state{NoContent}
